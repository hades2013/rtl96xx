OUTPUT_FORMAT("elf32-tradbigmips", "elf32-tradbigmips", "elf32-tradlittlemips")
OUTPUT_ARCH(mips)
ENTRY(_start)
SECTIONS
{
  /* set to uncached address */
  .ucflash (OTTO_FLASH_BASE | 0x20000000) :
  AT(OTTO_FLASH_BASE) {
    __LDS_START_O__(.text*)
    *(preloader.uc)
  }

  .sram (ALIGN((OTTO_SRAM_BASE + SIZEOF(.ucflash)), 4)) :
  AT(ALIGN((LOADADDR(.ucflash) + SIZEOF(.ucflash)), 4)) {
    __LDS_LIBSOC_O__(.text*)
    . = ALIGN(4);
    *(.flash_text)
    . = ALIGN(4);
    __LDS_LIBSOC_O__(.rodata*)
    . = ALIGN(4);
    __LDS_LIBSOC_O__(.data*)
    . = ALIGN(4);
    __LDS_LIBSOC_O__(.sdata*)
  }
  preloader_sram_start = LOADADDR(.ucflash);
  preloader_sram_size  = SIZEOF(.sram) + SIZEOF(.ucflash);

  . = ALIGN(4);
  .sram.bss :
  AT(0x0) {
    __LDS_LIBSOC_O__(.sbss*)
    . = ALIGN(4);
    __LDS_LIBSOC_O__(.bss*)
  }
  preloader_bss_start = ADDR(.sram.bss);
  preloader_bss_size  = SIZEOF(.sram.bss);

  .dram (OTTO_UBOOT_BASE) :
  AT(ALIGN((LOADADDR(.sram) + SIZEOF(.sram)), 4)) {
    *(EXCLUDE_FILE (__LDS_START_O__
                    __LDS_LIBSOC_O__)
                   .text*)

    . = ALIGN(4);
    *(EXCLUDE_FILE (__LDS_LIBSOC_O__) .data*)

    . = ALIGN(4);
    *(EXCLUDE_FILE (__LDS_LIBSOC_O__) .sdata*)

    . = ALIGN(4);
    *(EXCLUDE_FILE (__LDS_LIBSOC_O__) .rodata*)

    /* soft_patch */
    . = ALIGN(4);
    LS_sw_patch_start = .;
    KEEP(*(.soft_patch.0));
    KEEP(*(.soft_patch.1));
    KEEP(*(.soft_patch.2));
    KEEP(*(.soft_patch.3));
    KEEP(*(.soft_patch.4));
    KEEP(*(.soft_patch.5));
    KEEP(*(.soft_patch.6));
    KEEP(*(.soft_patch.7));
    KEEP(*(.soft_patch.8));
    KEEP(*(.soft_patch.9));
    KEEP(*(.soft_patch.10));
    KEEP(*(.soft_patch.11));
    KEEP(*(.soft_patch.12));
    KEEP(*(.soft_patch.13));
    KEEP(*(.soft_patch.14));
    KEEP(*(.soft_patch.15));
    KEEP(*(.soft_patch.16));
    KEEP(*(.soft_patch.17));
    KEEP(*(.soft_patch.18));
    KEEP(*(.soft_patch.19));
    KEEP(*(.soft_patch.20));
    KEEP(*(.soft_patch.21));
    KEEP(*(.soft_patch.22));
    KEEP(*(.soft_patch.23));
    LS_bootm_stack = .;
    KEEP(*(.soft_patch.99));
    LS_sw_patch_end = .;

    . = ALIGN(4);
	  __u_boot_cmd_start = .;
    *(.u_boot_cmd)
	  __u_boot_cmd_end = .;

    . = ALIGN(4);
    *(.rel.dyn)
  }
  
  uboot_start = LOADADDR(.dram);
  uboot_size  = SIZEOF(.dram);

  . = ALIGN(4);
  uboot_end_data = .;
  .sbss :
  AT(0x100000) {
    *(EXCLUDE_FILE (__LDS_LIBSOC_O__) .sbss*)
  }

	. = ALIGN(4);
	.bss :
  AT(0x200000) {
    *(EXCLUDE_FILE (__LDS_LIBSOC_O__) .bss*)
  }
  uboot_end = .;
}
